plugins {
    id 'java'
    id 'application'
    id "org.javamodularity.moduleplugin" version "1.8.9"
    id 'org.openjfx.javafxplugin' version '0.0.10'
    id "org.beryx.jlink" version "2.24.1"
}

group 'de.pedigreeProject'
version '1.0'

repositories {
    mavenCentral()
}

/*sourceCompatibility = '17'
targetCompatibility = '17'*/

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    applicationDefaultJvmArgs = ["-DLOG4J_SKIP_JANSI=false"]
    mainClass = 'de.pedigreeProject.MainApp'
    mainModule = 'pedigree.main'
}

javafx {
    version = '17'
    modules = ['javafx.controls', 'javafx.fxml', /*'javafx.web',*/ 'javafx.graphics']
}
dependencies {
    implementation('org.xerial:sqlite-jdbc:3.36.0.3')

    implementation 'org.apache.logging.log4j:log4j-core:2.17.1'
    implementation 'org.apache.logging.log4j:log4j-api:2.17.1'
    implementation 'org.fusesource.jansi:jansi:2.4.0'

    implementation('org.controlsfx:controlsfx:11.1.1')
    implementation('org.kordamp.ikonli:ikonli-javafx:12.3.1')
    implementation('org.kordamp.bootstrapfx:bootstrapfx-core:0.4.0')
    implementation 'org.kordamp.ikonli:ikonli-bootstrapicons-pack:12.3.1'
    implementation 'org.kordamp.ikonli:ikonli-icomoon-pack:12.3.1'
    implementation 'org.kordamp.ikonli:ikonli-materialdesign2-pack:12.3.1'

    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation('com.google.guava:guava:31.1-jre')
    implementation 'org.jetbrains:annotations:23.0.0'

    testImplementation 'com.opencsv:opencsv:5.6'
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.8.2")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.8.2")
    testImplementation('org.junit.jupiter:junit-jupiter-params:5.8.2')
    testImplementation 'org.mockito:mockito-inline:4.3.1'
    testImplementation 'org.mockito:mockito-junit-jupiter:4.3.1'
    testImplementation "org.testfx:testfx-core:4.0.16-alpha"
    testImplementation "org.testfx:testfx-junit5:4.0.16-alpha"

}
distributions {
    main {
        distributionBaseName = 'PedigreeProject'
    }
}

//jlink plugin settings
jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher{
        name = "PedigreeProject"
//        name = "pedigree.main"
//        name = "$moduleName"
    }
    forceMerge('log4j-api')
    jpackage {
        if (org.gradle.internal.os.OperatingSystem.current().windows) {
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']
            imageOptions += ['--win-console']
        }
    }
}
test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    //Options for TestFX
    jvmArgs = [
            '--add-opens', "pedigree.main/de.pedigreeProject=org.testfx.junit5",
//            '--add-opens', "$moduleName/de.pedigreeProject=org.testfx.junit5",
            '--add-opens', 'javafx.graphics/com.sun.javafx.application=org.testfx',
            '--add-opens', 'javafx.controls/com.sun.javafx.application=org.testfx',
            '--add-opens', 'javafx.controls/com.sun.javafx.scene.control=org.testfx'
    ]
}

